buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    jcenter()
    mavenCentral()
    google()
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'http://guardian.github.com/maven/repo-releases' }
}

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "com.transcendensoft.hedbanz"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        vectorDrawables.useSupportLibrary = true
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        project.ext.set("archivesBaseName", "Hedbanz-v" + versionName);
        multiDexEnabled true

    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        prodType {
            initWith(debug)
            buildConfigField("String", 'HOST_LINK', '\"http://35.198.153.34\"')
            buildConfigField("String", 'PORT_API', '\":8080/\"')
            buildConfigField("String", 'PORT_SOCKET', '\":9092\"')
        }
        devType {
            initWith(debug)
            buildConfigField("String", 'HOST_LINK', '\"http://77.47.204.201\"')
            buildConfigField("String", 'PORT_API', '\":8082/\"')
            buildConfigField("String", 'PORT_SOCKET', '\":9090\"')
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/notice'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/license'
    }
    lintOptions {
        abortOnError false
        enable 'BackButton'
        enable 'GradleDynamicVersion'
        enable 'NewerVersionAvailable'

        disable 'MissingTranslation'
        disable 'InvalidPackage'
        disable 'ExtraTranslation'
    }
    dexOptions {
        preDexLibraries = false
        javaMaxHeapSize "4g"
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    //Fabric
    implementation('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }

    //Android
    implementation 'com.android.support:support-v4:27.1.0'
    implementation 'com.android.support:appcompat-v7:27.1.0'
    implementation 'com.android.support:recyclerview-v7:27.1.0'
    implementation 'com.android.support:cardview-v7:27.1.0'
    implementation 'com.android.support:design:27.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    //Views
    implementation 'com.github.warkiz.widget:indicatorseekbar:1.2.9'
    implementation 'com.hannesdorfmann:adapterdelegates3:3.0.1'
    implementation 'com.github.apl-devs:appintro:v4.2.3'

    //Networking
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'

    //RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.1.6'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.jakewharton.rxbinding2:rxbinding-appcompat-v7:2.1.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.1.0'

    //Dagger 2
    implementation 'com.google.dagger:dagger-android:2.11'
    implementation 'com.google.dagger:dagger-android-support:2.11'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.11'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'

    //Glide
    implementation 'com.github.bumptech.glide:glide:4.6.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.6.1'

    //Firebase
    implementation 'com.google.firebase:firebase-auth:11.2.0'
    implementation 'com.google.android.gms:play-services-auth:11.2.0'
    implementation project(path: ':com-google-firebase-messaging')

    //Sockets
    implementation 'io.socket:socket.io-client:0.6.3'

    //Utils
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'com.jakewharton.timber:timber:4.5.1' //4.6.1 has bugs
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    prodTypeImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    devTypeImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    //Tests
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

apply plugin: 'com.google.gms.google-services'
